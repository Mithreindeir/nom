#Bubble Sort
bubble_sort = function(arr, comp):
	arr_size = size(arr)
	for j=0, j < arr_size, j++:
		for i = 0, i < (arr_size-1), i++:
			if not comp(arr[i], arr[i+1]):
				tmp = arr[i+1]
				arr[i+1]=arr[i]
				arr[i]=tmp
			end
		end
	end
end

merge_sort = function(arr, comp):
	s=size(arr)
	if s>1:
		mid = floor(s/2)
		a=array()
		for i=0, i < mid, i++:
			a[i]=arr[i]
		end
		b=array()
		for i=mid, i < s, i++:
			b[i-mid]=arr[i]
		end
		merge_sort(a, comp)
		merge_sort(b, comp)
		i=0
		j=0
		k=0
		ls = size(a)
		rs = size(b)
		while (i<ls) and (j<rs):
			if comp(a[i], b[j]):
				arr[k]=a[i]
				i++
			end
			else:
				arr[k]=b[j]
				j++
			end
			k++
		end
		while i < ls:
			arr[k]=a[i]
			i++
			k++
		end
		while j < rs:
			arr[k]=b[j]
			j++
			k++
		end
	end
end

fh=open("test.txt", "r")
str=read(fh)

#Returns a substring given a start and end
range = function(str1, low, high):
	expr = string()
	s = high-low
	expr = reserve(expr, s)
	for i=low, i<high, i++:
		expr[i-low] = str1[i]
	end
	return expr
end

#Alphabetical comparison function
alphabetical = function(str1, str2):
	low1 = size(str1)
	low2 = size(str2)
	low = low2
	if low1 < low2:
		low=low1
	end
	for i=0,i<low,i++:
		if str1[i] < str2[i]:
			return 1
		end
		elseif str1[i] > str2[i]:
			return 0
		end
	end
	return 0
end

a=array()
c=0
idx=0
#Separates Words by Spaces
for i=0, i<size(str), i++:
	idx=i
	while str[i] != " " and i < size(str) and str[i] != "\n":
		i++
	end
	a[c]=range(str, idx, i)
	c++
end
print("Wordlist: ", a, "\n")

copy = function(a):
	b=array()
	s = size(a)
	for i=0, i < s, i++:
		b[i]=a[i]
	end
	return b
end
b=copy(a)
print("\nSorting alphabetically...\n")
bt = clock()
merge_sort(a, alphabetical)
t = clock() - bt
print(a)
print("\nMerge sorted in ", t, " seconds\n\n")
bt = clock()
bubble_sort(b, alphabetical)
t = clock() - bt
print(b)
print("\nBubble sorted in ", t, " seconds\n")

close(fh)